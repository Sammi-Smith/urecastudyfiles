proc datasets library=work kill nolist nodetails; quit; 
dm out "clear"; dm log "clear";  
*******************************************************************************
*  Copyright Rho, Inc. 2017 all rights reserved                               *
*******************************************************************************;
*******************************************************************************
* Program created using SAS version 9.1 
*
* PROGRAMMER HISTORY:
*   Programmer(s)       Date(s)         Brief Description of Modifications
*  A. Calatroni         09/11/17        Created Program  
*  Andrew Moseby        06/12/18        Updated code for combining m81 with m84
*******************************************************************************;

*** 1. Set-up *****************************************************************;
options nodate pageno=1 mprint;
options orientation=landscape ls=96 ps=53 font=SASFONT 6;

*** Library, Titles (1,2) and Footnotes (1) Setup ***;
%inc "S:\RhoFED\ICAC\Studies\TemplateSetup.sas";
%TemplateSetup(/* Name of the program  */
               prog=bia,
               /* Location to save     */
               pgmpath=S:\RhoFED\ICAC\Studies\URECA\Prog\Derive,
               /* Study library to add */
               study=ureca);

*** Save program source code ***;
dm "file &gpgmpath\&gprog..sas replace";

*** 2. Main Body **************************************************************;                                                    

/* BIA Create */
proc sort data=master.w_bia   out=bia ; by studyid avisit; run ;
data bia;
 set bia;
 if status = "complete";
 rename w_bia_q1_1 = bia_perfromed
        w_bia_q1 = bia_resitance 
        w_bia_q2 = bia_reactance;
 keep studyid recruitid event avisit w_bia_q1_1 w_bia_q1 w_bia_q2;
run;

/* BIASC Create */
proc sort data=master.w_biasc out=biasc ; by studyid avisit; run;
data biasc;
 set biasc;
 if status = "complete";
 rename w_biasc_q1 = bia_metal
        w_biasc_q2 = bia_pacemaker
        w_biasc_q3 = bia_amputation
        w_biasc_q4 = bia_drink
        w_biasc_q5 = bia_thermal
        w_biasc_q5a1 = bia_tantrum
        w_biasc_q5a2 = bia_coldblue;
 keep studyid recruitid event avisit w_biasc_q1 -- w_biasc_q5a2;
run;

/* BIA + BIASC Merge */
data bia_01;
 merge biasc bia;
 by studyid avisit;
 avisit = input(put(avisit,$8.),8.);
run;
data bia_01;
 set bia_01 (rename = avisit = avisit_char);
  avisit = input(put(avisit_char,$8.),8.);
  if bia_perfromed = 1;
  if bia_metal = 0;
  if bia_pacemaker = 0;
  if bia_amputation = 0;
  drop avisit_char;
run;
 

proc sort data = bia_01; by studyid descending avisit; run;

data bia_01b;
 set bia_01;
 by studyid descending avisit;
 retain yr7counter;
 if avisit in (81 84) then yr7counter = sum(yr7counter, bia_perfromed); 
 else yr7counter = 0; 
 if yr7counter = 2 then delete; 
 if yr7counter = . and avisit = 81 then delete; 
 if yr7counter = 1 and avisit = 81 and not bia_perfromed then delete; 
 drop yr7counter;
 if avisit = 81 then avisit = 84;
run;

proc freq data=bia_01b;
 table studyid;
 where avisit in (81 84);
run;


proc sort data=bia_01b; by studyid avisit; run;

/* BMI Create */
proc sort data=derive.child_bmi out=child_bmi; by studyid avisit; run;
data child_bmi;
 set child_bmi;
 if height = . and weight = . then delete;
 keep studyid avisit year sex 
      AGEMOS SEX  HEIGHT  RECUMBNT  WEIGHT;
run;

/* BIA + BMI Create */
data bia_02;
 merge bia_01b (in=a) child_bmi;
 by studyid avisit;
 if a;

 
*hardcodes here have been removed to protect patient confidentiality;
      bia_resitance = 652;
      bia_reactance = 64;
  end;

  *hardcodes here have been removed to protect patient confidentiality;
  if bia_resitance < 250 or bia_resitance > 900 then delete; 
  if bia_reactance < 10  or bia_reactance > 120 then delete; 
run;

/* Create and merge standards */
/* From http://www.humankinetics.com/ProductSearchInside?isbn=9780736046558 */
/* Table 3.5; */
/* Height in cm,  */
/* weight (nude or minimal clothing) in kg,  */
/* resistance in ohms */
/* age & gender specific hydration constant from table 3.2 (value between 0.0 and 1.0, = %hydration/100) [ref 2] */

data pFFM;
*hardcodes here have been removed to protect patient confidentiality;
 input sex 2 avisit 3-9 pFFM 10-20;
datalines;
 1      0    80.6
 1      1    80.1
 1      2    79.7
 1      3    79.5
 1      6    78.9
 1      9    78.6
 1     12    78.3
 1     24    77.7
 1     36    77.4
 1     48    77.3
 1     60    77.1
 1     72    77.0
 1     81    76.9
 1     84    76.9
 1     96    76.8
 1    108    76.6
 1    120    76.5
 2      0    80.6
 2      1    80.1
 2      2    79.8
 2      3    79.6
 2      6    79.2
 2      9    78.9
 2     12    78.6
 2     24    77.7
 2     36    77.0
 2     48    76.6
 2     60    76.1
 2     72    75.8
 2     81    75.5
 2     84    75.5
 2     96    75.2
 2    108    74.9
 2    120    74.6
;

proc sort data=pFFM; by avisit sex;
proc sort data=bia_02; by avisit sex; run;

data bia_03;
 merge bia_02 (in=a) pFFM;
 by avisit sex;
 if a;
 /* 1) calculate TBW (kg) [ref 1] */
 /*    TBW = 0.593 ht2/R + 0.065wt + 0.04 */
 label TBW = "Total Body Water";
 TBW = 0.593*(height**2/bia_resitance) + 0.065*weight + 0.04;
 /* 2) calculate fat-free mass (kg) */
 /*    FFM = TBW/hydration constant */
 label FFM = "Fat-Free Mass (kg)";
 FFM = TBW / (pFFM/100); /* hydration constant */
 /* 3) calculate fat mass (kg) */
 /*    FM = wt â€“ FFM */
 label FM = "Fat Mass (kg)";
 FM = weight - FFM;
 /* 4) calculate % fat */
/*    %fat = (FM/wt)*100 */
 label pfat = "% Fat";
 pfat = (FM/weight)*100;
rename bia_resitance = bia_resistance;

*7/31/18 sl: remove year so that year will come from shell;
drop year;

  *delete negative pfat - 9/4/19 SL;
  if pfat < 0 then delete; 

run;

proc sort data=bia_03 out=derive.bia; by studyid avisit; run;

/*************************************************************************************
 Create Shell
**************************************************************************************/
data shell60;
 set derive.groups;

 site=substr(studyid, 4, 2);
 label    site="Study Site";
 format site $site.;

 do avisit = 12,24,36,48,60,72,84,96,108,120;
  if avisit in (12) then year=1;
  if avisit in (15, 18, 21, 24) then year=2;
  if avisit in (27, 30, 33, 36) then year=3;
  if avisit in (39, 42, 45, 48) then year=4;
  if avisit in (51, 54, 57, 60) then year=5;
  if avisit in (63, 66, 69, 72) then year=6;
  if avisit in (75, 78, 81, 84) then year=7;
  if avisit in (87, 90, 93, 96) then year=8;
  if avisit in (99,102,105,108) then year=9;
  if avisit in (111,114,117,120) then year=10;
  output;
 end;
 keep studyid recruitid site avisit year group;
run;

/*************************************************************************************
 Merge and derive
**************************************************************************************/
proc sort data=shell60;   by studyid avisit ; run ;
proc sort data=bia_03;    by studyid avisit ; run ;

data bia_04;
 merge shell60 bia_03 (in=c);
 by studyid avisit;
 format site $site.;
 if c=1 then bia=1; else bia=0;
run;

data derive.bia;
   set bia_04;

   label agemos = 'Age (months)'
         sex = 'Sex'
         pFFM = '% of FFM'
         avisit = 'Visit'
         year = 'Year'
         group = 'Atopic Status';
run;

proc sort data = derive.bia; by studyid avisit; run;

proc freq data=derive.bia; table avisit; run;

*** 3. Codebook ****************************************************************;
%include 'S:\BASESTAT\RhoUtil\gitGot.sas';
    %gitGot
        (repo = https://github.com/RhoInc/sas-codebook
        ,folder = Macros);

%codebook_generic
        (data = derive.bia
        ,pdfpath = S:/RhoFED/ICAC/Studies/URECA/Data/Derive/CBK);

*** 4. Save Log and Output ****************************************************;
quit;
dm out  "file &gpgmpath\&gprog..lst replace";
dm log  "file &gpgmpath\&gprog..log replace" log;


ods select Position;
proc contents data=derive.bia position; run;
run;
ods select default;