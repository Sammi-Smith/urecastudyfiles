proc datasets library=work kill nolist nodetails; quit;
dm out "clear"; dm log "clear";  
*******************************************************************************
*  Copyright Rho, Inc. 2011 all rights reserved                               *
*******************************************************************************;
*******************************************************************************
* Program created using SAS version 9.2 
*
* PROGRAMMER HISTORY:
*   Programmer(s)       Date(s)         Brief Description of Modifications
*   C. Visness          02/03/11        Created Program
*   K. Jaffee           10/24/11        Change template, var names
*   K. Jaffee           12/30/16        Update thru age 10 
*******************************************************************************;

*** 1. Set-up *****************************************************************;
options nodate pageno=1 mprint;
options orientation=landscape ls=96 ps=53 font=SASFONT 6;

*** Library, Titles (1,2) and Footnotes (1) Setup ***;
%inc "S:\RhoFED\ICAC\Studies\TemplateSetup.sas";
%TemplateSetup(/* Name of the program  */
               prog=home_env,
               /* Location to save     */
               pgmpath=S:\RhoFED\ICAC\Studies\URECA\Prog\Derive,
               /* Study library to add */
               study=ureca);

libname locked 'S:\RhoFED\ICAC_main\ICAC3\URECA\Statistics\Data\Derive';
*** Save program source code ***;
dm "file &gpgmpath\&gprog..sas replace";

*** 2. Main Body **************************************************************;
proc freq data=master.heq ;
   tables avisit event ; 
run ;

data env;
   set master.heq;
   if astudyid='.x' then delete;

***AIR CONDITIONING/HUMIDIFIERS ETC.***;

     **air conditioning in child's bedroom;
     if heq_q1a=1 or heq_q1b=1 then child_ac=1;
     else if heq_q1a=0 and heq_q1b=0 then child_ac=0;
     else child_ac=.;

    rename 
      avisit=avisit_char
         heq_q1a=child_centralac 
         heq_q1b=child_roomac
         heq_q1c=aircleaner
         heq_q1d=dehumidif
         heq_q1e=humidifier
         HEQ_q2=heat_mainsource
         HEQ_q2a=heat_spec_mainsource
         HEQ_q3a=heat_radiator
         HEQ_q3b=heat_central
         HEQ_q3c=heat_electricbase
         HEQ_q3d=heat_elecspace
         HEQ_q3e=heat_kerosenespace
         HEQ_q3f=heat_stoveoven
         HEQ_q3g=heat_woodstove
         HEQ_q3h=heat_fireplace
         HEQ_q3i=heat_other
         HEQ_q3i1=heat_specother
         HEQ_q4=gas_stove
         HEQ_q4a=burn_pilotlt
         HEQ_q5=home_waterprobs
         HEQ_q6a=home_mice
         HEQ_q6b=home_rats
         HEQ_q7=home_roach
         HEQ_q7a=home_roach_day
         HEQ_q8=dog_any 
         HEQ_q8a=dog_max 
         HEQ_q8b=dog_curr
         HEQ_q8b1=dog_childrm
         HEQ_q9=cat_any
         HEQ_q9a=cat_max 
         HEQ_q9b=cat_curr
         HEQ_q9b1=cat_childrm
         HEQ_q10=petrodent_any
         HEQ_q10a=petrodent_curr
         HEQ_q10a1=petrodent_childrm
         HEQ_q11=daycare_10hr
         HEQ_q12=daycare_numhrs
         HEQ_q13=daycare_numkids
         HEQ_q14=daycare_heatsource
         HEQ_q14a=daycare_spec_heatsource
         HEQ_q15=daycare_gas_stove
         HEQ_q16=daycare_waterprobs
         HEQ_q17a=daycare_mice
         HEQ_q17b=daycare_rats 
         HEQ_q17c=daycare_roach
         HEQ_q18=daycare_dog
         HEQ_q19=daycare_cat
         HEQ_q20a=daycare_carpet
         HEQ_q20b=daycare_arearug
         HEQ_q20c=daycare_linol
         HEQ_q20d=daycare_wood
         HEQ_q20e=daycare_concrete 
         HEQ_q20f=daycare_otherfloor
         HEQ_q20f1=daycare_spec_floor
         HEQ_q21=other_10hr
         HEQ_q22=other_numhrs
         HEQ_q23=other_numkids
         HEQ_q24=other_heatsource
         HEQ_q24a=other_spec_heatsource
         HEQ_q25=other_gas_stove
         HEQ_q26=other_waterprobs
         HEQ_q27a=other_mice 
         HEQ_q27b=other_rats
         HEQ_q27c=other_roach
         HEQ_q28=other_dog
         HEQ_q29=other_cat 
         HEQ_q30a=other_carpet 
         HEQ_q30b=other_arearug
         HEQ_q30c=other_linol
         HEQ_q30d=other_wood
         HEQ_q30e=other_concrete
         HEQ_q30f=other_otherfloor
         HEQ_q30f1=other_spec_floor ;
run ;

data env2 ;
   set env ;

   avisit = input(put(avisit_char,$8.),8.);

***HEATING SOURCES***;

     **radiators;
     if heat_mainsource=1 OR heat_radiator=1 then radiator=1;
     else if heat_mainsource ne . then radiator=0;

     **forced air vents in home;
     if heat_mainsource=2 OR heat_central=1 then airvents=1;
     else if heat_mainsource ne . then airvents=0;

     **electric baseboards;
     if heat_mainsource=3 OR heat_electricbase=1 then baseboards=1;
     else if heat_mainsource ne . then baseboards=0;

     **electric space heater;
     if heat_mainsource=4 OR heat_elecspace=1 then elec_sp=1;
     else if heat_mainsource ne . then elec_sp=0;

     **kerosene space heater;
     if heat_mainsource=5 OR heat_kerosenespace=1 then kero_sp=1;
     else if heat_mainsource ne . then kero_sp=0;

     **open stove or oven;
     if heat_mainsource=6 OR heat_stoveoven=1 then openstov=1;
     else if heat_mainsource ne . then openstov=0;

     **wood burning stove;
     if heat_mainsource=7 OR heat_woodstove=1 then woodstov=1;
     else if heat_mainsource ne . then woodstov=0;

     **fireplace;
     if heat_mainsource=8 OR heat_fireplace=1 then fireplace=1;
     else if heat_mainsource ne . then fireplace=0;

***RODENTS***;

     **report by caretaker of problems with mice or rats;
     if home_mice=1 or home_rats=1 then home_rodents=1;
     else if home_mice=0 and home_rats=0 then home_rodents=0;
     else home_rodents=.;

***COCKROACHES***;

    if home_roach=1 and home_roach_day=1 then home_roachday=1;
     else if home_roach=0 or home_roach_day=0 then home_roachday=0;
     else home_roachday=.;

***PETS***;

     **account for skip patterns;
     if dog_any=1 and dog_curr=1 then home_dog_curr=1;
     else if dog_any=0 or dog_curr=0 then home_dog_curr=0;
     else home_dog_curr=.;
    if dog_any=0 then dog_max=0;
    if dog_curr=0 then dog_childrm=0;

     if cat_any=1 and cat_curr=1 then home_cat_curr=1;
     else if cat_any=0 or cat_curr=0 then home_cat_curr=0;
     else home_cat_curr=.;
    if cat_any=0 then cat_max=0;
    if cat_curr=0 then cat_childrm=0;

     if petrodent_any=1 and petrodent_curr=1 then home_othpet_curr=1;
     else if petrodent_any=0 or petrodent_curr=0 then home_othpet_curr=0;
     else home_othpet_curr=.;
    if home_othpet_curr=0 then petrodent_childrm=0;


     **pets reported by caretaker ;
     if home_dog_curr=1 or home_cat_curr=1 or home_othpet_curr=1 then home_pets_curr=1;
     else if home_dog_curr=0 and home_cat_curr=0 and home_othpet_curr=0 then home_pets_curr=0;
     else home_pets_curr=.;

     label home_rodents= 'Problem with mice or rats in home?'
          home_roachday= 'Problem with roaches during the day?' 

           home_dog_curr= 'Dog currently living in home?'
           home_cat_curr= 'Cat currently living in home?'
           home_othpet_curr='Other furry pet currently living in home?'
           home_pets_curr='Report of any furry pet currently in home?'

           child_ac="Air conditioning in child's bedroom?"

           radiator='Radiators used for heating home?'
           airvents='Forced air used for heating home?'
           baseboards='Electric baseboards used for heating home?'
           openstov ='Open stove used for heating home?'
           elec_sp ='Electric space heater used in home?'
           kero_sp ='Kerosene space heater used in home?'
           woodstov='Wood stove used for heating home?'
           fireplace='Fireplace used for heating home?' ;

     format home_waterprobs home_roachday
            home_rodents home_dog_curr home_cat_curr home_othpet_curr home_pets_curr 
            child_ac dehumidif aircleaner humidifier
            radiator airvents baseboards openstov elec_sp kero_sp woodstov fireplace
            yesnofm.
            ;

***Combine exposures across home and daycare***;
   
   *just cockroaches and pets for now;

   if home_roach=1 or daycare_roach=1 or other_roach=1 then anyres_roach=1;
     else if home_roach=0 and (daycare_roach ne 1 or daycare_10hr=0) 
          and (other_roach ne 1 or other_10hr=0) then anyres_roach=0;
     else anyres_roach=.; 
   if dog_any=1 or daycare_dog=1 or other_dog=1 then anyres_dog=1;
     else if dog_any=0 and (daycare_dog ne 1 or daycare_10hr=0) 
          and (other_dog ne 1 or other_10hr=0) then anyres_dog=0;
     else anyres_dog=.; 
   if cat_any=1 or daycare_cat=1 or other_cat=1 then anyres_cat=1;
     else if cat_any=0 and (daycare_cat ne 1 or daycare_10hr=0) 
          and (other_cat ne 1 or other_10hr=0) then anyres_cat=0;
     else anyres_cat=.; 

   label anyres_roach='Any cockroach exposure in last year'
         anyres_dog='Any dog exposure in last year'
         anyres_cat='Any cat exposure in last year';
   format anyres_roach anyres_dog anyres_cat yesnofm.;

   drop avisit_char ;
run ;

data env3 ;
   set env2 ;

   avisit_actual=avisit ; 
   if avisit in (81,84) then avisit=84 ;
run ;

proc freq; tables avisit * avisit_actual/list missing; run;

proc sort data=env3 nodupkey out=clean dupout=dups ; by studyid avisit ; run ;


/*************************************************************************************
 Create Shell - thru age 10
**************************************************************************************/
data shell;
 set derive.groups;

 site=substr(studyid, 4, 2);
 label    site="Study Site";
 format site $site.;

 do avisit = 3,12,24,36,48,60,72,84,96,108,120;
  if avisit in ( 3,  12) then year=1;
  if avisit = 24 then year=2;
  if avisit = 36 then year=3;
  if avisit = 48 then year=4;
  if avisit = 60 then year=5;
  if avisit = 72 then year=6;
  if avisit = 84 then year=7;
  if avisit = 96 then year=8;
  if avisit = 108 then year=9;
  if avisit = 120 then year=10;
  output;
 end;
 keep studyid recruitid site avisit year;
run;

proc sort data=shell    ; by studyid avisit ; run ;
proc sort data=env3     ; by studyid avisit ; run ;

**save derived dataset;
data derive.home_env;
   merge shell env3(in=b) ;
   by studyid avisit ;

   if b=1 then home_env=1 ; else home_env=0 ;
   drop aPI;
run;
/**/
/*data locked.home_env;*/
/*   set derive.home_env;*/
/*run;*/

proc freq; tables home_env*avisit; run;
/**** 3. Codebook ****************************************************************;*/
/*%inc "O:\Asthma\Apps\Cbk\codebook.sas";*/
/*%codebook(file=derive.home_env,*/
/*          pdfname=home_env_yr1,*/
/*          formats=library,*/
/*        save=F,*/
/*        ifs=%str(if avisit=%"12%"),*/
/*        clean=T,*/
/*          pdfloc=%str(S:/RhoFED/ICAC/Studies/URECA/Data/Derive/CBK));*/

%include 'S:\BASESTAT\RhoUtil\gitGot.sas';
    %gitGot
        (repo = https://github.com/RhoInc/sas-codebook
        ,folder = Macros);

%codebook_generic
        (data = derive.home_env
        ,pdfpath = S:/RhoFED/ICAC/Studies/URECA/Data/Derive/CBK);


*** 4. Save Log and Output ****************************************************;
quit;
dm out  "file &gpgmpath\&gprog..lst replace";
dm log  "file &gpgmpath\&gprog..log replace" log;



ods select Position;
proc contents data=derive.home_env position; run;
run;
ods select default;