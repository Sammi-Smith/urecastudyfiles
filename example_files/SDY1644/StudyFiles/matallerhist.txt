proc datasets library=work kill nolist nodetails; quit;
dm out "clear"; dm log "clear";  
*******************************************************************************
*  Copyright Rho, Inc. 2011 all rights reserved                               *
*******************************************************************************;
*******************************************************************************
* Program created using SAS version 9.2 
*
* PROGRAMMER HISTORY:
*   Programmer(s)       Date(s)         Brief Description of Modifications
*   C. Visness         06/13/05        Created Program
*                  03/26/07      Added code to reconcile versions 1.4 and 1.5
*   K. Jaffee         02/27/12      Change template, var names
*   Alexandre Lockhart 05JUL2017    Reran with new codebooks and general checks
*******************************************************************************;

*** 1. Set-up *****************************************************************;
options nodate pageno=1 mprint;
options orientation=landscape ls=96 ps=53 font=SASFONT 6;

*** Library, Titles (1,2) and Footnotes (1) Setup ***;
%inc "S:\RhoFED\ICAC\Studies\TemplateSetup.sas";
%TemplateSetup(/* Name of the program  */
               prog=matallerhist,
               /* Location to save     */
               pgmpath=S:\RhoFED\ICAC\Studies\URECA\Prog\Derive,
               /* Study library to add */
               study=ureca);

*** Save program source code ***;
dm "file &gpgmpath\&gprog..sas replace";

*** 2. Main Body **************************************************************;

/*************************************************************************************
 Rename all variables in MRAH and get rid of mothers never enrolled in URECA
**************************************************************************************/
data mathist;
   set master.mrah;
   if studyid='.m' then delete;

   avisit2=0 ;

   rename MRAH_q1 = mom_noseprobs_nocold 
         MRAH_q1a = mom_noseprobs_timeyr
         MRAH_q1b1= mom_noseprobs_spring 
         MRAH_q1b2=mom_noseprobs_summer
         MRAH_q1b3=mom_noseprobs_fall
         MRAH_q1b4=mom_noseprobs_winter
         MRAH_q2 = mom_noseprobs_animaldustmold

         MRAH_q3=mom_whzever
         MRAH_q4=mom_whz12mo
         MRAH_q4a=mom_whz_whencold 
         MRAH_q4b=mom_whz_nocold
         MRAH_q4c=mom_whz_animaldustmold

         MRAH_q4d1=mom_whzmeds12mo_albut
         MRAH_q4d2=mom_whzmeds12mo_inhsteroid
         MRAH_q4d3=mom_whzmeds12mo_oralsteroid
         MRAH_q4d4=mom_whzmeds12mo_singulair
         MRAH_q4d5=mom_whzmeds12mo_advair
         MRAH_q4d6=mom_whzmeds12mo_other
         MRAH_q4d6a=mom_whzmeds12mo_otherspec

         MRAH_q5=mom_everasthma
         MRAH_q5a=mom_asthmastart
         MRAH_q5b=mom_asthmadiag
         MRAH_q5c=mom_stillasthma

         MRAH_q5d1=mom_whzastmeds12mo_albut
         MRAH_q5d2=mom_whzastmeds12mo_inhsteroid
         MRAH_q5d3=mom_whzastmeds12mo_oralsteroid
         MRAH_q5d4=mom_whzastmeds12mo_singulair
         MRAH_q5d5=mom_whzastmeds12mo_advair
         MRAH_q5d6=mom_whzastmeds12mo_other
         MRAH_q5d6a=mom_whzastmeds12mo_otherspec

         MRAH_q5d1_1=mom_astmeds12mo_albut
         MRAH_q5d2_1=mom_astmeds12mo_inhsteroid
         MRAH_q5d3_1=mom_astmeds12mo_oralsteroid
         MRAH_q5d4_1=mom_astmeds12mo_singulair
         MRAH_q5d5_1=mom_astmeds12mo_advair
         MRAH_q5d6_1=mom_astmeds12mo_other
         MRAH_q5d6a_1=mom_astmeds12mo_otherspec

         MRAH_q6=dad_everasthma
         MRAH_q6a=dad_asthmastart
         MRAH_q6b=dad_asthmadiag
          MRAH_q6c=dad_stillasthma

         MRAH_q6d1=dad_astmeds12mo_albut
         MRAH_q6d2=dad_whzmeds12mo_inhsteroid
         MRAH_q6d3=dad_whzmeds12mo_oralsteroid
         MRAH_q6d4=dad_whzmeds12mo_singulair
         MRAH_q6d5=dad_whzmeds12mo_advair
         MRAH_q6d6=dad_whzmeds12mo_other
         MRAH_q6d6a=dad_whzmeds12mo_otherspec

         MRAH_q7=mom_everhay
         MRAH_q7a=mom_haydiag
         MRAH_q7b=mom_stillhay

         MRAH_q7c1=mom_haymeds12mo_antihist
         MRAH_q7c2=mom_haymeds12mo_nasalsteroid
         MRAH_q7c3=mom_haymeds12mo_oralsteroid
         MRAH_q7c4=mom_haymeds12mo_singulair
         MRAH_q7c5=mom_haymeds12mo_other
         MRAH_q7c5a=mom_haymeds12mo_otherspec

         MRAH_q8=dad_everhay

         MRAH_q9=mom_everecz
         MRAH_q9a=mom_eczdiag
         MRAH_q9b=mom_stillecz
         MRAH_q9c1=mom_eczmeds12mo_oint
         MRAH_q9c2=mom_eczmeds12mo_othercream
         MRAH_q9c3=mom_eczmeds12mo_other
         MRAH_q9c3a=mom_eczmeds12mo_otherspec

         MRAH_q10=dad_everecz

         MRAH_q11=otherkids
         MRAH_q11a=otherkids_asthma
         MRAH_q11a1=otherkids_asthmadiag
         MRAH_q11b=otherkids_hay
         MRAH_q11b1=otherkids_haydiag
         MRAH_q11c=otherkids_ecz
         MRAH_q11c1=otherkids_eczdiag
         MRAH_q12=momwt_prepreg
         ;

   drop avisit ;
run ;


/*************************************************************************************
 Set skip patterns to 0 instead of .n/missing
**************************************************************************************/
data mathist2 ;
   set mathist ;
 
****
NOT SURE AT THIS POINT IF IT WILL BE APPROPRIATE TO ALLOW YESES TO COMBINED QUESTION TO COUNT
AS YESES TO INDIVIDUAL QUESTIONS, OR IF IT IS BETTER TO COMBINE THE INDIVIDUAL QUESTIONS, OR
WHETHER WE SHOULD DROP THE COMBINED QUESTION. WE WILL RECONSIDER WHEN THERE IS MORE DATA
C. VISNESS 06/13/05;
****
3/26/07: Decision looks ok to apply medications answered to "asthma or wheeze" to just "wheeze"
IF mother answered she had wheeze -- i.e. the skip pattern follows so that if she had v. 1.5 
that she would have been asked the question. Use the var names as in v 1.5
NOTE: only versions 1.4 and 1.5 were ever actually used to collect data;

   avisit=avisit2 ;

   if input(version, 8.)=1.4 and mom_whz12mo=1 then do;
   mom_whzmeds12mo_albut       = mom_whzastmeds12mo_albut;
   mom_whzmeds12mo_inhsteroid  = mom_whzastmeds12mo_inhsteroid;
   mom_whzmeds12mo_oralsteroid = mom_whzastmeds12mo_oralsteroid;
   mom_whzmeds12mo_singulair   = mom_whzastmeds12mo_singulair;
   mom_whzmeds12mo_advair      = mom_whzastmeds12mo_advair;
   mom_whzmeds12mo_other       = mom_whzastmeds12mo_other;
   end;

   if input(version, 8.)=1.4 and mom_everasthma=1 then do;
   mom_astmeds12mo_albut       = mom_whzastmeds12mo_albut;
   mom_astmeds12mo_inhsteroid  = mom_whzastmeds12mo_inhsteroid;
   mom_astmeds12mo_oralsteroid = mom_whzastmeds12mo_oralsteroid;
   mom_astmeds12mo_singulair   = mom_whzastmeds12mo_singulair;
   mom_astmeds12mo_advair      = mom_whzastmeds12mo_advair;
   mom_astmeds12mo_other       = mom_whzastmeds12mo_other;
   end;

   drop mom_whzastmeds12mo_albut mom_whzastmeds12mo_inhsteroid mom_whzastmeds12mo_oralsteroid 
       mom_whzastmeds12mo_singulair mom_whzastmeds12mo_advair mom_whzastmeds12mo_other mom_whzastmeds12mo_otherspec;


   if mom_whzever=0 then do; *ever wheezed;
   mom_whz12mo=0;
   mom_whz_whencold=0;
   mom_whz_nocold=0;
   mom_whz_animaldustmold=0;
   mom_whzmeds12mo_albut=0;
   mom_whzmeds12mo_inhsteroid=0;
   mom_whzmeds12mo_oralsteroid=0;
   mom_whzmeds12mo_singulair=0;
   mom_whzmeds12mo_advair=0;
   mom_whzmeds12mo_other=0;
   end;
   if mom_whz12mo=0 then do; *wheezed in past year;
   mom_whz_whencold=0;
   mom_whz_nocold=0;
   mom_whz_animaldustmold=0;
   mom_whzmeds12mo_albut=0;
   mom_whzmeds12mo_inhsteroid=0;
   mom_whzmeds12mo_oralsteroid=0;
   mom_whzmeds12mo_singulair=0;
   mom_whzmeds12mo_advair=0;
   mom_whzmeds12mo_other=0;
   end;
   if mom_everasthma=0 then do;
   mom_asthmastart=0;
   mom_asthmadiag=0;
   mom_stillasthma=0;
   mom_astmeds12mo_albut=0;
   mom_astmeds12mo_inhsteroid=0;
   mom_astmeds12mo_oralsteroid=0;
   mom_astmeds12mo_singulair=0;
   mom_astmeds12mo_advair=0;
   mom_astmeds12mo_other=0;
   end;
   if dad_everasthma=0 then do;
   dad_asthmastart=0;
   dad_asthmadiag=0;
   dad_stillasthma=0;
   dad_astmeds12mo_albut=0;
   dad_whzmeds12mo_inhsteroid=0;
   dad_whzmeds12mo_oralsteroid=0;
   dad_whzmeds12mo_singulair=0;
   dad_whzmeds12mo_advair=0;
   dad_whzmeds12mo_other=0;
   end;
   if mom_everhay=0 then do;
   mom_haydiag=0;
   mom_stillhay=0;
   mom_haymeds12mo_antihist=0;
   mom_haymeds12mo_nasalsteroid=0;
   mom_haymeds12mo_oralsteroid=0;
   mom_haymeds12mo_singulair=0;
   mom_haymeds12mo_other=0;
   end;
   if mom_everecz=0 then do;
   mom_eczdiag=0;
   mom_stillecz=0;
   mom_eczmeds12mo_oint=0;
   mom_eczmeds12mo_othercream=0;
   mom_eczmeds12mo_other=0;
   end;

   drop avisit2 astudyid aRcrtID ;
run ;

/*************************************************************************************
 Check Duplicates - none as of July 5, 2017
**************************************************************************************/
proc sort nodupkey data=mathist2 out=mathist3 dupout=dups; by studyid avisit;
run;



/*************************************************************************************
 Create Shell
**************************************************************************************/
data shell00;
 set derive.groups;

 site=substr(studyid, 4, 2);
 label    site="Study Site";
 format site $site.;

 do avisit = 0;
  if avisit in (0) then year=0;
 output;
 end;
 keep studyid recruitid site avisit year;
run;

/*************************************************************************************
 Merge and derive
**************************************************************************************/
proc sort data=shell00  ; by studyid avisit ; run;
proc sort data=mathist3 ; by studyid avisit ; run;

data derive.matallerhist;
   merge shell00 mathist3(in=c);
   by studyid avisit;
   format site $site.;
   if c=1 then matallerhist=1; else matallerhist=0;
   drop aPI;
run;

*** 3. Codebook ****************************************************************;
/*%inc "O:\Asthma\Apps\Cbk\codebook.sas";
%codebook(file=derive.matallerhist,
          pdfname=matallerhist,
          formats=library,
        save=F,
        clean=T,
          pdfloc=%str(S:/RhoFED/ICAC/Studies/URECA/Data/Derive/CBK)); */
%include 'S:\BASESTAT\RhoUtil\gitGot.sas';
    %gitGot
        (repo = https://github.com/RhoInc/sas-codebook
        ,folder = Macros);

%codebook_generic
        (data = derive.matallerhist
        ,pdfpath = S:/RhoFED/ICAC/Studies/URECA/Data/Derive/CBK);



*** 4. Save Log and Output ****************************************************;
quit;
dm out  "file &gpgmpath\&gprog..lst replace";
dm log  "file &gpgmpath\&gprog..log replace" log;


ods select Position;
proc contents data=derive.matallerhist position; run;
run;
ods select default;