%include 'S:\BASESTAT\RhoUtil\gridReset.sas';
%gridReset(S:\RhoFED\ICAC\Studies\URECA\Prog\Derive);

/*----------------------- Copyright 2017, Rho, Inc.  All rights reserved. ------------------------\

  Study:       ICAC07 URECA

   Program:    mom_lifestress.sas

     Purpose:  Create derive dataset from mlsc clinical data

     Input:    S:\RhoFED\ICAC\Stuides\URECA\Data\Master\mlsc.sas7bdat

     Output:   S:\RhoFED\ICAC_main\ICAC3\URECA\Statistics\Data\Derive\mom_lifestress.sas7bdat

     Macros:   %Template_Setup

/-------------------------------------------------------------------------------------------------\
  Program history:
\-------------------------------------------------------------------------------------------------/

    Date       Programmer          Description
    ---------  ------------------  --------------------------------------------------------------
    17JUL2017  Andrew Moseby       Create

\------------------------------------------------------------------------------------------------*/

*-----   1. Setup   -----*;

options nodate pageno=1 mprint;
options orientation=landscape ls=96 ps=53 font=SASFONT 6;

*** Library, Titles (1,2) and Footnotes (1) Setup ***;
%inc "S:\RhoFED\ICAC\Studies\TemplateSetup.sas";
%TemplateSetup(/* Name of the program  */
               prog=mom_lifestress,
               /* Location to save     */
               pgmpath=S:\RhoFED\ICAC\Studies\URECA\Prog\Derive,
               /* Study library to add */
               study=ureca);

libname locked 'S:\RhoFED\ICAC_main\ICAC3\URECA\Statistics\Data\Derive';
*** Save program source code ***;
dm "file &gpgmpath\&gprog..sas replace";

*-----   2. Main Body   -----*;

proc sort data = master.mlsc out = mlsc; by studyid avisit; run;

data mlsc2;
   set mlsc;
   by studyid avisit;
   where status ^= 'missing';
   avisit1 = input(avisit, 8.);

   lifestress_score = sum(of MLSC_q1 - MLSC_q11); 
   worst_event_score = sum(of MLSC_q15 - MLSC_q21);
  * if MLSC_q14a = '' then worst_event_timing = ;
   traumatic_score = sum(of MLSC_q12 - MLSC_q13);

   if sum(of MLSC_q14a1 - MLSC_q14a5) > 1 then worst_event_timing = 'multiple timepoints';
      else if MLSC_q14a1 = 1 then worst_event_timing = 'prior to pregnancy';
      else if MLSC_q14a2 = 1 then worst_event_timing = 'during pregnancy';
      else if MLSC_q14a3 = 1 then worst_event_timing = 'child less than 1 year old';
      else if MLSC_q14a4 = 1 then worst_event_timing = 'child 1 to 3 years old';
      else if MLSC_q14a5 = 1 then worst_event_timing = 'child older than 3 years old';

   drop avisit MLSC_q14a1 - MLSC_q14a5;
   rename mlsc_q22 = affected_life;


run;

*-----   Check for dups - none as of 17JUN2017   -----*;
proc sort data = mlsc2(rename = (avisit1=avisit)) out = mlsc3 nodupkey dupout = dups; by studyid avisit; run;

data shell;
   set derive.groups;

   site = substr(studyid, 4, 2);
   label site = 'Study Site';
   format site $site.;

   do avisit = 60, 72, 108;
      if avisit in (60)    then year = 5;
      if avisit in (72)    then year = 6;
      if avisit in (108)   then year = 9;
      output;
   end;
   keep studyid recruitid site avisit year;
run;

proc sort data = shell; by studyid avisit; run;
proc sort data = mlsc3; by studyid avisit; run;

data mom_lifestress;
   merge shell mlsc3(in = done);
   by studyid avisit;
   if done then mom_lifestress = 1; else mom_lifestress = 0;
   drop aPI;
run;

proc sort data = mom_lifestress out = derive.mom_lifestress; by studyid avisit; run;

*-----   3. Codebook   -----*;
%include 'S:\BASESTAT\RhoUtil\gitGot.sas';
    %gitGot
        (repo = https://github.com/RhoInc/sas-codebook
        ,folder = Macros);

%codebook_generic
        (data = derive.mom_lifestress
        ,pdfpath = S:/RhoFED/ICAC/Studies/URECA/Data/Derive/CBK);

*** 4. Save Log and Output ****************************************************;
quit;
dm out  "file &gpgmpath\&gprog..lst replace";
dm log  "file &gpgmpath\&gprog..log replace" log;

ods select Position;
proc contents data=derive.mom_lifestress position; run;
run;
ods select default;