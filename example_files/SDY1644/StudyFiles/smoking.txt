proc datasets library=work kill nolist nodetails; quit;
dm out "clear"; dm log "clear";  
*******************************************************************************
*  Copyright Rho, Inc. 2011 all rights reserved                               *
*******************************************************************************;
*******************************************************************************
* Program created using SAS version 9.1 
*
* PROGRAMMER HISTORY:
*   Programmer(s)       Date(s)         Brief Description of Modifications
*   K. Jaffee         03/21/11      Create
*******************************************************************************;

*** 1. Set-up *****************************************************************;
options nodate pageno=1 mprint;
options orientation=landscape ls=96 ps=53 font=SASFONT 6;

*** Library, Titles (1,2) and Footnotes (1) Setup ***;
%inc "S:\RhoFED\ICAC\Studies\TemplateSetup.sas";
%TemplateSetup(/* Name of the program  */
               prog=smoking,
               /* Location to save     */
               pgmpath=S:\RhoFED\ICAC\Studies\URECA\Prog\Derive,
               /* Study library to add */
               study=ureca);

*** Save program source code ***;
dm "file &gpgmpath\&gprog..sas replace";

*** 2. Main Body **************************************************************;
proc format /*library=library*/;
 value cig      0      = '0'
            0<-<6  = '<=5'
            6-<11  = '6-10'
            11-<21 = '11-20'
            21-30  = '21-30'
            31-40  = '31-40'
            40-high= '40+';
 value alc      0      ='0'
            0<-1   ='<=1'
            1<-3   ='2-3'
            3<-high='+3';
run;

%inc "S:\RhoFED\ICAC\Studies\URECA\Prog\Derive\smokhist.sas";
%inc "S:\RhoFED\ICAC\Studies\URECA\Prog\Derive\smokupdate.sas";

proc sort data=derive.smokhist   out=sh; by studyid ; run ;
proc sort data=derive.smokupdate out=su; by studyid avisit; run ;


/*************************************************************************************
 Set 2 smoking datasets  - prenatal and quarterly calls ;
**************************************************************************************/
data sh_su_set ;
 set sh su ;
run ;

proc sort data=sh_su_set; by studyid avisit; run;

***Drop out shell variables and delete those with missing events because we will merge the shell in later
   and need to identify who has smoking data with "smoking" variable ;
/*************************************************************************************
 Drop out shell variables;
**************************************************************************************/
data smoking_1 ; 
 set sh_su_set ;
 drop site recruitid /*group*/ ; 
 if smokhist=0   then delete;
 if smokupdate=0 then delete;
run ;

/*************************************************************************************
 Create Shell
**************************************************************************************/
data shell72;
 set derive.groups ;

 site=substr(studyid, 4, 2);
 label    site="Study Site";
 format site $site.;

 do avisit = 0,3,6,9,12,15,18,21,24,27,30,33,36,39,42,45,48,51,54,57,60,63,66,69,72,
           75,78,81,84,87,90,93,96,99,102,105,108,111,114,117,120;
  if avisit in ( 0)             then year=0;
  if avisit in ( 3,  6,  9, 12) then year=1;
  if avisit in (15, 18, 21, 24) then year=2;
  if avisit in (27, 30, 33, 36) then year=3;
  if avisit in (39, 42, 45, 48) then year=4;
  if avisit in (51, 54, 57, 60) then year=5;
  if avisit in (63, 66, 69, 72) then year=6;
  if avisit in (75, 78, 81, 84) then year=7;
  if avisit in (87, 90, 93, 96) then year=8;
  if avisit in (99, 102, 105, 108) then year=9;
  if avisit in (111, 114, 117, 120) then year=10;
 output;
 end;
 keep studyid recruitid site avisit year;
run;

/*************************************************************************************
 Merge and derive
**************************************************************************************/
proc sort data=smoking_1   ; by studyid avisit; run ;
proc sort data=shell72     ; by studyid avisit; run ;

data derive.smoking;
 merge shell72 smoking_1(in=b) derive.respondent;
 by studyid avisit ;
 if b=1 then smoking = 1 ; else smoking = 0 ;
 drop smokhist smokupdate;
run;

*** 3. Codebook ****************************************************************;
%include 'S:\BASESTAT\RhoUtil\gitGot.sas';
    %gitGot
        (repo = https://github.com/RhoInc/sas-codebook
        ,folder = Macros);

%codebook_generic
        (data = derive.smoking
        ,pdfpath = S:/RhoFED/ICAC/Studies/URECA/Data/Derive/CBK);

/*%inc "O:\Asthma\Apps\Cbk\codebook.sas";*/
/*%codebook(file=derive.smoking,*/
/*          pdfname=smoking,*/
/*          formats=library,*/
/*        save=F,*/
/*        clean=T,*/
/*          pdfloc=%str(S:/RhoFED/ICAC/Studies/URECA/Data/Derive/CBK));*/

*** 4. Save Log and Output ****************************************************;
quit;
dm out  "file &gpgmpath\&gprog..lst replace";
dm log  "file &gpgmpath\&gprog..log replace" log;


ods select Position;
proc contents data=derive.smoking position; run;
run;
ods select default;