proc datasets library=work kill nolist nodetails; quit;
dm out "clear"; dm log "clear";  
*******************************************************************************
*  Copyright Rho, Inc. 2011 all rights reserved                               *
*******************************************************************************;
*******************************************************************************
* Program created using SAS version 9.2 
*
* PROGRAMMER HISTORY:
*   Programmer(s)       Date(s)         Brief Description of Modifications
*   C. Visness          05/24/06        Created Program
*   K. Jaffee           04/14/14        Change template, var names, update
*******************************************************************************;

*** 1. Set-up *****************************************************************;
options nodate pageno=1 mprint;
options orientation=landscape ls=96 ps=53 font=SASFONT 6;

*** Library, Titles (1,2) and Footnotes (1) Setup ***;
%inc "S:\RhoFED\ICAC\Studies\TemplateSetup.sas";
%TemplateSetup(/* Name of the program  */
               prog=home_obs,
               /* Location to save     */
               pgmpath=S:\RhoFED\ICAC\Studies\URECA\Prog\Derive,
               /* Study library to add */
               study=ureca);

libname locked 'S:\RhoFED\ICAC_main\ICAC3\URECA\Statistics\Data\Derive';
*** Save program source code ***;
dm "file &gpgmpath\&gprog..sas replace";

proc freq data=master.heo ;
   tables avisit event ; 
run ;

proc sort data=master.heo out=heo ; by studyid ; run ;

data heo2 ;
   set heo ;
if astudyid='.x' then delete;

   rename HEO_q3=house_type 
         HEO_q3a=house_type_spec
         HEO_q4=num_rooms
         HEO_q5=room_childsleep
         HEO_q5a=room_childsleep_spec 
         HEO_q6a=otherroom_roachstain
         HEO_q6b=otherroom_roach
         HEO_q6c=otherroom_mousedrop
         HEO_q7=otherroom_moisture
         HEO_q8a=kitchen_mildew
         HEO_q8b=bathroom_mildew
         HEO_q8c=famroom_mildew
         HEO_q9=otherroom_musty
         HEO_q10=otherroom_cigs
         HEO_q11=otherroom_cigsmell
         HEO_q12=otherroom_hepa
         HEO_q13a=kitchen_carpet
         HEO_q13b=kitchen_rug
         HEO_q13c=kitchen_scatrug
         HEO_q13d=kitchen_tile
         HEO_q13e=kitchen_wood
         HEO_q13f=kitchen_concrete
         HEO_q13g=kitchen_other
         HEO_q13g1=kitchen_otherspec
         HEO_q14a=bathroom_carpet
         HEO_q14b=bathroom_rug
         HEO_q14c=bathroom_scatrug
         HEO_q14d=bathroom_tile
         HEO_q14e=bathroom_wood
         HEO_q14f=bathroom_concrete
         HEO_q14g=bathroom_other
         HEO_q14g1=bathroom_otherspec
         HEO_q15a=famroom_carpet
         HEO_q15b=famroom_rug
         HEO_q15c=famroom_scatrug
         HEO_q15d=famroom_tile
         HEO_q15e=famroom_wood
         HEO_q15f=famroom_concrete
         HEO_q15g=famroom_other
         HEO_q15g1=famroom_otherspec
         HEO_q16a=bedroom_roachstain
         HEO_q16b=bedroom_roach
         HEO_q16c=bedroom_mousedrop
         HEO_q17=bedroom_moisture
         HEO_q18=bedroom_mildew
         HEO_q19=bedroom_musty
         HEO_q20=bedroom_cigs
         HEO_q21=bedroom_cigsmell
         HEO_q22=bedroom_numbeds
         HEO_q23a=bedroom_carpet
         HEO_q23b=bedroom_rug
         HEO_q23c=bedroom_scatrug
         HEO_q23d=bedroom_tile
         HEO_q23e=bedroom_wood
         HEO_q23f=bedroom_concrete
         HEO_q23g=bedroom_other
         HEO_q23g1=bedroom_otherspec
         HEO_q24a=bedroom_blinds
         HEO_q24b=bedroom_curtains
         HEO_q24c=bedroom_otherwindow
         HEO_q24c1=bedroom_otherwindowspec
         HEO_q25=bedroom_hepa 
         aVisit=aVisit_char ;
run ;

proc freq data=heo2 ;
   tables event * avisit_char /list missing ;
   format event $20.;
run ;

proc print data=heo2 ;   
   var studyid event ;
   where avisit_char="r" or avisit_char="ah" ;
run ;

data heo3 ;
   set heo2 ;

   length kitchen_flooring bathroom_flooring famroom_flooring bedroom_flooring bedroom_windowtrt $100. ;

   if aVisit_char="3"  then avisit=12 ;
   if aVisit_char="rh" then avisit=12 ; **Rescheduled visit 3mo ;
   if aVisit_char="ah" then avisit=12 ; **Rescheduled visit 3mo ;
   if aVisit_char="h1" then avisit=24 ;
   if aVisit_char="h2" then avisit=36 ;
   if aVisit_char="h3" then avisit=48 ;
   if aVisit_char="h4" then avisit=60 ;

   ***Corrections ;
*hardcodes here have been removed to protect patient confidentiality;


   ***Roach observation in any room ;
   home_obs_roach = MAX(otherroom_roachstain,otherroom_roach,bedroom_roachstain,bedroom_roach) ;

   ***Mice droppings observation in any room ;
   home_obs_mice = MAX(otherroom_mousedrop,bedroom_mousedrop) ;

   ***Mildew observation in any room ;
   home_obs_mildew = MAX(kitchen_mildew,bathroom_mildew,famroom_mildew,bedroom_mildew) ;

   ***Moisture observation in any room ;
   home_obs_moisture = MAX(otherroom_moisture,bedroom_moisture) ;

   ***Musty smell observation in any room ;
   home_obs_musty = MAX(otherroom_musty,bedroom_musty) ;

   ***Evidence of water damage (combine musty, mildew and moisture) ;
   home_obs_waterdam = MAX(home_obs_mildew,home_obs_moisture,home_obs_musty) ;

   ***Evidence of cigarettes/cig smell in bedroom ;
   if nmiss(bedroom_cigs,bedroom_cigsmell)^=2 
      then home_obs_bedroom_cigs = MAX(bedroom_cigs,bedroom_cigsmell) ;

   ***Evidence of cigarettes/cig smell in any part of home ;
   if nmiss(otherroom_cigs,otherroom_cigsmell,bedroom_cigs,bedroom_cigsmell)^=4 
      then home_obs_cigs = MAX(otherroom_cigs,otherroom_cigsmell,bedroom_cigs,bedroom_cigsmell) ;

   ***Flooring in kitchen ;
   if nmiss(kitchen_carpet,kitchen_rug,kitchen_scatrug,kitchen_tile,kitchen_wood,kitchen_concrete,kitchen_other)^=7 then do;
      if MAX(kitchen_carpet,kitchen_rug,kitchen_scatrug,kitchen_tile,kitchen_wood,kitchen_concrete,kitchen_other)=1 then do ;   
         if kitchen_carpet=1 then kitchen_flooring="Kitchen floor: wall-to-wall carpeting" ;
         if MAX(kitchen_rug,kitchen_scatrug)=1 then kitchen_flooring="Kitchen floor: rug or scatter rug" ;
         if kitchen_tile=1 then kitchen_flooring="Kitchen floor: tile" ;
         if kitchen_wood=1 then kitchen_flooring="Kitchen floor: wood" ;
         if kitchen_concrete=1 then kitchen_flooring="Kitchen floor: concrete" ;
         if kitchen_other=1 then kitchen_flooring="Kitchen floor: other" ;
      end ;
      if MAX(kitchen_carpet,kitchen_rug,kitchen_scatrug,kitchen_tile,kitchen_wood,kitchen_concrete,kitchen_other)>1 then do ;   
         kitchen_flooring="Kitchen floor: combination of flooring types" ;
      end ;
   end;

   ***Flooring in bathroom ;
   if nmiss(bathroom_carpet,bathroom_rug,bathroom_scatrug,bathroom_tile,bathroom_wood,bathroom_concrete,bathroom_other)^=7 then do;
      if MAX(bathroom_carpet,bathroom_rug,bathroom_scatrug,bathroom_tile,bathroom_wood,bathroom_concrete,bathroom_other)=1 then do ;   
         if bathroom_carpet=1 then bathroom_flooring="Bathroom floor: wall-to-wall carpeting" ;
         if MAX(bathroom_rug,bathroom_scatrug)=1 then bathroom_flooring="Bathroom floor: rug or scatter rug" ;
         if bathroom_tile=1 then bathroom_flooring="Bathroom floor: tile" ;
         if bathroom_wood=1 then bathroom_flooring="Bathroom floor: wood" ;
         if bathroom_concrete=1 then bathroom_flooring="Bathroom floor: concrete" ;
         if bathroom_other=1 then bathroom_flooring="Bathroom floor: other" ;
      end ;
      if MAX(bathroom_carpet,bathroom_rug,bathroom_scatrug,bathroom_tile,bathroom_wood,bathroom_concrete,bathroom_other)>1 then do ;   
         bathroom_flooring="Bathroom floor: combination of flooring types" ;
      end ;
   end;

   ***Flooring in family room ;
   if nmiss(famroom_carpet,famroom_rug,famroom_scatrug,famroom_tile,famroom_wood,famroom_concrete,famroom_other)^=7 then do;
      if MAX(famroom_carpet,famroom_rug,famroom_scatrug,famroom_tile,famroom_wood,famroom_concrete,famroom_other)=1 then do ;   
         if famroom_carpet=1 then famroom_flooring="Family room floor: wall-to-wall carpeting" ;
         if MAX(famroom_rug,famroom_scatrug)=1 then famroom_flooring="Family room floor: rug or scatter rug" ;
         if famroom_tile=1 then famroom_flooring="Family room floor: tile" ;
         if famroom_wood=1 then famroom_flooring="Family room floor: wood" ;
          if famroom_concrete=1 then famroom_flooring="Family room floor: concrete" ;
          if famroom_other=1 then famroom_flooring="Family room floor: other" ;
      end ;
      if MAX(famroom_carpet,famroom_rug,famroom_scatrug,famroom_tile,famroom_wood,famroom_concrete,famroom_other)>1 then do ;   
         famroom_flooring="Family room floor: combination of flooring types" ;
      end ;
   end;

   ***Flooring in bedroom ;
   if nmiss(bedroom_carpet,bedroom_rug,bedroom_scatrug,bedroom_tile,bedroom_wood,bedroom_concrete,bedroom_other)^=7 then do;
      if MAX(bedroom_carpet,bedroom_rug,bedroom_scatrug,bedroom_tile,bedroom_wood,bedroom_concrete,bedroom_other)=1 then do ;   
         if bedroom_carpet=1 then bedroom_flooring="Bedroom floor: wall-to-wall carpeting" ;
         if MAX(bedroom_rug,bedroom_scatrug)=1 then bedroom_flooring="Bedroom floor: rug or scatter rug" ;
         if bedroom_tile=1 then bedroom_flooring="Bedroom floor: tile" ;
         if bedroom_wood=1 then bedroom_flooring="Bedroom floor: wood" ;
          if bedroom_concrete=1 then bedroom_flooring="Bedroom floor: concrete" ;
          if bedroom_other=1 then bedroom_flooring="Bedroom floor: other" ;
      end ;
      if MAX(bedroom_carpet,bedroom_rug,bedroom_scatrug,bedroom_tile,bedroom_wood,bedroom_concrete,bedroom_other)>1 then do ;   
         bedroom_flooring="Bedroom floor: combination of flooring types" ;
      end ;
   end;

   if bedroom_blinds=1 and bedroom_curtains=1 then bedroom_windowtrt="Bedroom windows: blinds and curtains" ;
   if bedroom_blinds=1 and bedroom_curtains=0 then bedroom_windowtrt="Bedroom windows: blinds" ;
   if bedroom_blinds=0 and bedroom_curtains=1 then bedroom_windowtrt="Bedroom windows: curtains" ;
   if bedroom_blinds=0 and bedroom_curtains=0 and bedroom_otherwindow=1 then bedroom_windowtrt="Bedroom windows: other treatment" ;

   ***Handling duplicates  ;
 *hardcodes here have been removed to protect patient confidentiality;
   ***Delete the original 3mo HE because they did a rescheduled visit ;
*hardcodes here have been removed to protect patient confidentiality;


   label kitchen_flooring = "Type of flooring in kitchen" 
        bathroom_flooring = "Type of flooring in bathroom" 
        famroom_flooring = "Type of flooring in family room" 
        bedroom_flooring = "Type of flooring in bedroom" 
        home_obs_roach="Home observation: any roaches or roach stains in home"
        home_obs_mice="Home observation: any mouse droppings in home"
        home_obs_mildew="Home observation: any mildew in home"
        home_obs_moisture="Home observation: any moisture in home"
        home_obs_musty="Home observation: any musty smell in home"
        home_obs_waterdam="Home observation: any evidence of water damage in home"
        home_obs_bedroom_cigs="Home observation: any evidence of cigs, ashtrays, or tobacco smell in bedroom"
        home_obs_cigs="Home observation: any evidence of cigs, ashtrays, or tobacco smell in home" 
        bedroom_windowtrt="Type of window treatment in bedroom" ;

   *keep studyid event aCompletionDate aVisit house_type house_type_spec num_rooms room_childsleep room_childsleep_spec otherroom_hepa bedroom_numbeds
       bedroom_hepa kitchen_flooring bathroom_flooring famroom_flooring bedroom_flooring home_obs_roach
       home_obs_mice home_obs_mildew home_obs_moisture home_obs_musty home_obs_waterdam home_obs_bedroom_cigs
       home_obs_cigs bedroom_windowtrt ;

run ;

***No duplicates as of 12/30/2016 ;
***All duplicates checked and verified by Cindy on 07/14/17;
proc sort data=heo3 out=heo_clean nodupkey  dupout=dups  ; by studyid avisit ; run ;

/*************************************************************************************
 Create Shell 
**************************************************************************************/
data shell;
 set derive.groups;

 site=substr(studyid, 4, 2);
 label    site="Study Site";
 format site $site.;

 do avisit = 12,24,36,48,60;
  if avisit = 12 then year=1;
  if avisit = 24 then year=2;
  if avisit = 36 then year=3;
  if avisit = 48 then year=4;
  if avisit = 60 then year=5;
  output;
 end;
 keep studyid recruitid site avisit year;
run;

proc sort data=shell    ; by studyid avisit ; run ;
proc sort data=heo_clean     ; by studyid avisit ; run ;

**save derived dataset;
data derive.home_obs;
   merge shell heo_clean(in=b) ;
   by studyid avisit ;

   if b=1 then home_obs=1 ; else home_obs=0 ;
   drop aPI;
run;
/**/
/*data locked.home_obs;*/
/*   set derive.home_obs;*/
/*run;*/

*** Codebook ****************************************************************;

%include 'S:\BASESTAT\RhoUtil\gitGot.sas';
    %gitGot
        (repo = https://github.com/RhoInc/sas-codebook
        ,folder = Macros);

%codebook_generic
        (data = derive.home_obs
        ,pdfpath = S:/RhoFED/ICAC/Studies/URECA/Data/Derive/CBK);
quit;
dm out  "file &gpgmpath\&gprog..lst replace";
dm log  "file &gpgmpath\&gprog..log replace" log;

ods select Position;
proc contents data=derive.home_obs position; run;
run;
ods select default;
