proc datasets library=work kill nolist nodetails; quit;
dm out "clear"; dm log "clear";  
*******************************************************************************
*  Copyright Rho, Inc. 2011 all rights reserved                               *
*******************************************************************************;
*******************************************************************************
* Program created using SAS version 9.2 
*
* PROGRAMMER HISTORY:
*   Programmer(s)       Date(s)         Brief Description of Modifications
*   K Jaffee            07/20/2011      Created Derive Dataset     
*   K Jaffee            07/10/2012      Add IL-16
*******************************************************************************;

*** 1. Set-up *****************************************************************;
options nodate pageno=1 mprint;
options orientation=landscape ls=96 ps=53 font=SASFONT 6;

*** Library, Titles (1,2) and Footnotes (1) Setup ***;
%inc "S:\RhoFED\ICAC\Studies\TemplateSetup.sas";
%TemplateSetup(/* Name of the program  */
               prog=labdata,
               /* Location to save     */
               pgmpath=S:\RhoFED\ICAC\Studies\URECA\Prog\Derive,
               /* Study library to add */
               study=ureca);

libname locked 'S:\RhoFED\ICAC_main\ICAC3\URECA\Statistics\Data\Derive';
*** Save program source code ***;
dm "file &gpgmpath\&gprog..sas replace";
/**/
/*proc format cntlin=library.studyformats; run;*/
/*libname library;*/
*** 2. Main Body **************************************************************;

/*************************************************************************************
 BEGIN SCD14
**************************************************************************************/

/*************************************************************************************
 Create variables 
**************************************************************************************/
data scd14_1;
 set master.ppscd14;
                                                                     
 if avisit="b"  then avisit="0";
*hardcodes here have been removed to protect patient confidentiality;
 *if status="complete";
 rename aProcDate = aProcDate_scd14; label aProcDate = 'sCD14 Processing Date';
 rename PPSCD14_q1=mean_conc; label PPSCD14_q1="sCD14 Mean Concentration";
 rename PPSCD14_q2=std_conc;  label PPSCD14_q2="sCD14 Standard Deviation of Concentration";
 rename PPSCD14_q3=cv_conc;   label PPSCD14_q3="sCD14 Coefficient of Variation (%)";
 rename PPSCD14_q4=dilution;  label PPSCD14_q4="sCD14 Dilution";
 rename PPSCD14_q5=adj_conc;  label PPSCD14_q5="sCD14 Adjusted Concentration";
run;
 
data scd14_2 ;
 set scd14_1 (rename=(avisit=avisit_char)); 
 avisit = input(put(avisit_char,$8.),8.);
 drop avisit_char version status sampleid recruitid astudyid arcrtid ati;
 format mean_conc std_conc 8.3 cv_conc 8.1 dilution 8.0 adj_conc 8.3;
run ;   

/*************************************************************************************
 Check Duplicates - there are none as of 7/20/11
**************************************************************************************/
proc sort nodupkey data=scd14_2  out=scd14_3  dupout=dups; by studyid avisit; 
run;

/*************************************************************************************
 BEGIN VITAMIN D
**************************************************************************************/
proc sort data=master.cbp   out=cbp; by recruitid ; run ;
proc sort data=master.cbsc  out=cbsc; by recruitid; run ;
proc sort data=master.chbsc out=chbsc; by recruitid avisit; run ;
proc sort data=master.vdr   out=vdr; by recruitid avisit ; run ;

data vdr_cord ;
   set vdr ;
   where avisit="b" ;

   if vdr_q1b1=.b or .z < vdr_q1b1 < 4 
   then do; 
   vitD_D3  =3; 
   end; 

   else vitD_D3=vdr_q1b1;
run ;

proc means data=vdr_cord n mean nmiss ;
   var vitD_D3 ;
run ;   

data chbsc2 ;
   set chbsc ;
   where avisit ne "12" ;
   keep recruitid avisit CHBSC_q3a ;
run ;

data vdr_other ;
   set vdr ;
   if avisit ne "b" ;
run ;

data vdr_other2 ;
   length avisit $4;
   merge vdr_other(in=a) chbsc2 ;
   by recruitid avisit ;
   if a=1 ;

   if vdr_q1b1 > .z then vitD_D3_d=1;

   **assign lower values;

   if vdr_q1b1=.b or .z < vdr_q1b1 < 4 
   then do; 
   vitD_D3  =3; 
   vitD_D3_d=0; 
   end; 
   else vitD_D3=vdr_q1b1;

   if VDR_q1d1=".b" then epi3=0.9 ;
   else epi3=INPUT(VDR_q1d1,5.) ;

   rename CHBSC_q3a=date_collected_vitd ;
   drop recruitid version SampleID aStudyid aRcrtID ;
run ;

proc sort data=cbp      ; by recruitid ; run ;
proc sort data=cbsc     ; by recruitid ; run ;
proc sort data=vdr_cord ; by recruitid ; run ;

data cord_vitd ;
   merge     vdr_cord  (keep=recruitid avisit vitD_D3) 
               cbp       (keep=recruitid studyid CBP_q1a cbp_q1c cbp_q13_1)
               cbsc      (keep=recruitid cbsc_q3);

   by recruitid;
   if studyid ne '.m';

   **get old dilution factor for a few 'obsolete' samples;
   if cbp_q13_1=.o then do;
      if cbsc_q3=1 then cbp_q13_1_new=(cbp_q1c+20)/cbp_q1c;
      if cbsc_q3=2 and cbp_q1c=40 then cbp_q13_1_new=2;
    end;
   else cbp_q13_1_new=cbp_q13_1 ;
   **new conversion of dilution factor;
   if missing(cbp_q13_1_new)=0
      then dilution=((cbp_q13_1_new-1)+0.5)/0.5;

   if nmiss(vitD_D3,dilution) =0 then vitD_D3=vitD_D3*dilution  ; else vitD_D3=.;

   if vitD_D3 > .z then vitD_D3_d=1;

   rename CBP_q1a=date_collected_vitd ;

   keep studyid avisit vitD_D3 vitD_D3_d CBP_q1a ;
run ;

data vitd;
   set vdr_other2 cord_vitd ;

   if studyid='.m' then delete ; 
   if avisit = " " then delete ;

   *epi3=VDR_q1d1 ;
   volume=VDR_q1e1 ;

   if avisit="b" then avisit="0" ;

   label   vitD_D3_d='Detectable Vit D3'
         vitD_D3='Vitamin D3 (ng/ml) - visit 0 is adjusted for dilution'
         epi3='Vitamin D, 3 epi (ng/mL)'
         volume='Vitamin D, Volume (mL)'
         date_collected_vitd = 'Vitamin D Date Collected';

   format vitD_D3_d  yesnofm.;

run ;

proc means data=vitd ;
   var vitD_D3 ;
   where avisit="0" ;
run ;

data vitd_1 ;
   set vitd (rename=(avisit=avisit_char)); 

   avisit = input(put(avisit_char,$8.),8.);

   *if vitD_D3 = . then delete ;

   drop avisit_char status vdr_q1a1 vdr_q1b1 vdr_q1c1 VDR_q1d1 VDR_q1e1 ;
run ;

proc means data=vitd_1 ;
   var vitD_D3 ;
   where avisit=0 ;
run ;

/*************************************************************************************
 BEGIN VIRAL SEROLOGY
**************************************************************************************/
proc sort data=master.vs out=vs ; by studyid ; run ;

proc freq data=vs ;
   tables vs_q1 vs_q3 ;
run ;

proc contents data=vs; run;
data vs2 ;
   set vs (rename=(avisit=avisit_char)); 
    avisit = input(put(avisit_char,$8.),8.);

   rename vs_q1 = cmv_rawcut
         vs_q2 = cmv_date
         vs_q3 = ebv_rawcut 
         vs_q4 = ebv_date ;

   drop recruitid status astudyid sampleid version avisit_char ;
run ;

/*************************************************************************************
 BEGIN TSLP
**************************************************************************************/
proc sort data=master.tslp out=tslp ; by studyid ; run ;

data tslp2 ;
   set tslp (rename=(aVst=avisit_char)); 

    avisit = input(put(avisit_char,$8.),8.);

   if studyid=' ' then delete;
    if tslp_q1 = .b then tslp_level = 0.010;
   else tslp_level = tslp_q1 ;                                                                         
   rename  sampleid = sampleid_tslp ; 
   format TSLP_q1 TSLP_level 15.3 ;
   drop recruitid version status aStudyID avisit_char TSLP_q1 ;
   label sampleid = 'TSLP SampleID'
         TSLP_level = 'Mean TSLP';
run;

/*************************************************************************************
 BEGIN IL-16
**************************************************************************************/
proc sort data=master.il16 out=il16 ; by studyid ; run ;

data il16_2 ;
   set il16 ;
   
   **Per Marina Tuzova email 7/10/12, drop these 2 observations they failed in the lab ;
*hardcodes here have been removed to protect patient confidentiality;

   if avisit="b" then avisit2=0 ;
   if avisit="36" then avisit2=36 ;

   il_16 = IL16_q1 ;
   il_16_l = log10(IL16_q1) ;

   bdnf = IL16_q2 ;
   bdnf_l = log10(IL16_q2);

   drop avisit astudyid recruitid version status IL16_q1 IL16_q2;

   label    il_16 = 'IL-16 (pg/ml)' 
       il_16_l = 'IL-16 (pg/ml) (log10)' 
       bdnf = 'BDNF (pg/ml)'
       bdnf_l = 'BDNF (pg/ml) (log10)'
       sampleid = 'IL-16 SampleID';
run ;

data il16_3 ;
   set il16_2 ;

   rename avisit2=avisit ;
run ;

/*************************************************************************************
 BEGIN FCERI - Dan Jackson substudy
**************************************************************************************/
proc sort data=master.jlr out=jlr ; by aVisit ; run ;

data jlr2 ;
   set jlr ;

   if avisit="b" then avisit="0" ;

   mdc_mfi=input(JLR_q1,10.) ;
   mdc_gm=input(JLR_q2,10.) ;
   mdc_mol=input(JLR_q3,10.) ;
   mdc_freq=input(JLR_q4,10.) ;
   mdc_percentpos=input(JLR_q5,10.) ;

   pdc_mfi=input(JLR_q6,10.) ;
   pdc_gm=input(JLR_q7,10.) ;
   pdc_mol=input(JLR_q8,10.) ;
   pdc_freq=input(JLR_q9,10.) ;
   pdc_percentpos=input(JLR_q10,10.) ;

   baso_mfi=input(JLR_q11,10.) ;
   baso_gm=input(JLR_q12,10.) ;
   baso_mol=input(JLR_q13,10.) ;
   baso_freq=input(JLR_q14,10.) ;
   baso_percentpos=input(JLR_q15,10.) ;

   mono_mfi=input(JLR_q16,10.) ;
   mono_gm=input(JLR_q17,10.) ;
   mono_mol=input(JLR_q18,10.) ;
   mono_freq=input(JLR_q19,10.) ;
   mono_percentpos=input(JLR_q20,10.) ;

   label mdc_mfi="Mdc Fcer1 MFI" 
         mdc_gm="mDC Fcer1GeMEAN"
        mdc_mol="mDC mol per cell" 
        mdc_freq="mDC/Freq of Total (%)" 
        mdc_percentpos="% FceRI pos mDC" 

        pdc_mfi="Pdc Fcer1 MFI" 
         pdc_gm="pDC Fcer1GeMEAN"
        pdc_mol="pDC mol per cell" 
        pdc_freq="pDC/Freq of Total (%)" 
        pdc_percentpos="% FceRI pos pDC" 

        baso_mfi="Baso Fcer1 MFI" 
         baso_gm="Baso Fcer1GeMEAN"
        baso_mol="Baso mol per cell" 
        baso_freq="Baso/Freq of Total (%)" 
        baso_percentpos="% FceRI pos Baso" 

        mono_mfi="Mono Fcer1 MFI" 
         mono_gm="Mono Fcer1GeMEAN"
        mono_mol="Mono mol per cell" 
        mono_freq="Mono/Freq of Total (%)" 
        mono_percentpos="% FceRI pos Mono" ;

   drop astudyid recruitid JLR_q1 --    JLR_q19 version status;
run ;

data jlr3 ;
   set jlr2 (rename=(avisit=avisit_char)); 

    avisit = input(put(avisit_char,$8.),8.);

  *hardcodes here have been removed to protect patient confidentiality;

   drop avisit_char ;
run ;

/*************************************************************************************
 Create Shell
**************************************************************************************/
data shell48;
 set derive.groups;

 site=substr(studyid, 4, 2);
 label    site="Study Site";
 format site $site.;

 do avisit = 0,12,24,36,48 ;
  if avisit in (0)              then year=0;
  if avisit in ( 3,  6,  9, 12) then year=1;
  if avisit in (15, 18, 21, 24) then year=2;
  if avisit in (27, 30, 33, 36) then year=3;
  if avisit in (39, 42, 45, 48) then year=4;
 output;
 end;
 keep studyid recruitid site avisit year group;
run;

/*************************************************************************************
 Merge and derive
**************************************************************************************/
proc sort data=shell48; by studyid avisit ; run ;
proc sort data=scd14_3; by studyid avisit ; run ;
proc sort data=vitd_1 ; by studyid avisit ; run ;
proc sort data=vs2    ; by studyid avisit ; run ;
proc sort data=tslp2  ; by studyid avisit ; run ;
proc sort data=il16_3 ; by studyid avisit ; run ;
proc sort data=jlr3   ; by studyid avisit ; run ;

data derive.labdata;
   merge shell48 scd14_3 (in=a) vitd_1 (in=b) vs2 (in=c) tslp2 (in=d) il16_3 (in=e) jlr3 (in=f) ;
   by studyid avisit;
   format site $site.;
   if a=1 then scd14=1; else scd14=0;
   if b=1 then vitd =1; else vitd =0;
   if c=1 then vs   =1; else vs   =0;
   if d=1 then tslp =1; else tslp =0;
   if e=1 then il16 =1; else il16 =0;
   if f=1 then fceri=1; else fceri=0 ;
   if (a or b or c or d or e or f) then labdata = 1;
run;

/**/
/*data locked.labdata;*/
/*   set derive.labdata;*/
/*run;*/

*** 3. Codebook ****************************************************************;
/*%inc "O:\Asthma\Apps\Cbk\codebook.sas";*/
/*%codebook(file=derive.labdata,*/
/*          pdfname=labdata,*/
/*          formats=library,*/
/*        save=F,*/
/*        clean=T,*/
/*          pdfloc=%str(S:/RhoFED/ICAC/Studies/URECA/Data/Derive/CBK));*/

%include 'S:\BASESTAT\RhoUtil\gitGot.sas';
%gitGot
        (repo = https://github.com/RhoInc/sas-codebook
        ,folder = Macros);

%codebook_generic
        (data = derive.labdata
        ,pdfpath = S:/RhoFED/ICAC/Studies/URECA/Data/Derive/CBK);



*** 4. Save Log and Output ****************************************************;
quit;
dm out  "file &gpgmpath\&gprog..lst replace";
dm log  "file &gpgmpath\&gprog..log replace" log;


ods select Position;
proc contents data=derive.labdata position; run;
run;
ods select default;

